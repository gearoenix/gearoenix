SET(GX_ENGINE_SRC_DIR ${CMAKE_SOURCE_DIR}/gearoenix)

MESSAGE(STATUS "Gearoenix game engine source directory is ${GX_ENGINE_SRC_DIR}.")

FILE(GLOB_RECURSE GX_ENGINE_SRC CONFIGURE_DEPENDS "*.cpp" "*.hpp")
SET(GX_ENGINE_TEST_SRC ${GX_ENGINE_SRC})
LIST(FILTER GX_ENGINE_SRC EXCLUDE REGEX ".*-test\\.hpp$")
LIST(FILTER GX_ENGINE_TEST_SRC INCLUDE REGEX ".*-test\\.hpp$")

SOURCE_GROUP(TREE ${GX_ENGINE_SRC_DIR} FILES ${GX_ENGINE_SRC})
SOURCE_GROUP(TREE ${GX_ENGINE_SRC_DIR} FILES ${GX_ENGINE_TEST_SRC})

SET(GX_ENGINE_TEST_SRC ${GX_ENGINE_TEST_SRC} PARENT_SCOPE)

SET(GX_ENGINE_IMGUI_SRC
        ${GX_IMGUI_PATH}/imgui.cpp
        ${GX_IMGUI_PATH}/imgui_demo.cpp
        ${GX_IMGUI_PATH}/imgui_draw.cpp
        ${GX_IMGUI_PATH}/imgui_tables.cpp
        ${GX_IMGUI_PATH}/imgui_widgets.cpp
        ${GX_IMGUI_PATH}/backends/imgui_impl_opengl3.cpp
        ${GX_IMGUI_PATH}/backends/imgui_impl_vulkan.cpp
        ${GX_IMGUI_PATH}/misc/cpp/imgui_stdlib.cpp)
SET_SOURCE_FILES_PROPERTIES(${GX_ENGINE_IMGUI_SRC} PROPERTIES COMPILE_FLAGS ${GX_NO_WARNING_FLAG})
SOURCE_GROUP("imgui" FILES ${GX_ENGINE_IMGUI_SRC})
SET(GX_ENGINE_SRC ${GX_ENGINE_SRC} ${GX_ENGINE_IMGUI_SRC})

SET(GX_ENGINE_IMGUI_FB_SRC ${GX_IMGUI_FB_PATH}/ImGuiFileDialog.cpp)
SET_SOURCE_FILES_PROPERTIES(${GX_ENGINE_IMGUI_FB_SRC} PROPERTIES COMPILE_FLAGS ${GX_NO_WARNING_FLAG})
SOURCE_GROUP("imgui-fb" FILES ${GX_ENGINE_IMGUI_FB_SRC})
SET(GX_ENGINE_SRC ${GX_ENGINE_SRC} ${GX_ENGINE_IMGUI_FB_SRC})

SET(GX_ENGINE_IMGIZMO_SRC
        ${GX_IMGIZMO_PATH}/GraphEditor.cpp
        ${GX_IMGIZMO_PATH}/ImCurveEdit.cpp
        ${GX_IMGIZMO_PATH}/ImGradient.cpp
        ${GX_IMGIZMO_PATH}/ImGuizmo.cpp
        ${GX_IMGIZMO_PATH}/ImSequencer.cpp)
SET_SOURCE_FILES_PROPERTIES(${GX_ENGINE_IMGIZMO_SRC} PROPERTIES COMPILE_FLAGS ${GX_NO_WARNING_FLAG})
SOURCE_GROUP("imgizmo" FILES ${GX_ENGINE_IMGIZMO_SRC})
SET(GX_ENGINE_SRC ${GX_ENGINE_SRC} ${GX_ENGINE_IMGIZMO_SRC})

ADD_LIBRARY(GearoenixGameEngine STATIC ${GX_ENGINE_SRC})

FOREACH (GX_LIB ${GX_DEBUG_LIBS})
    TARGET_LINK_LIBRARIES(GearoenixGameEngine debug ${GX_LIB})
ENDFOREACH ()

FOREACH (GX_LIB ${GX_RELEASE_LIBS})
    TARGET_LINK_LIBRARIES(GearoenixGameEngine optimized ${GX_LIB})
ENDFOREACH ()

ADD_CUSTOM_TARGET(GearoenixAssets)

FUNCTION(GX_ENGINE_PREPARE_TARGET GX_CURRENT_TARGET)
    MESSAGE(STATUS "Preparing target ${GX_CURRENT_TARGET}")
    IF (APPLE)
        GX_METAL_PREPARE_TARGET(${GX_CURRENT_TARGET})
        TARGET_LINK_LIBRARIES(${GX_CURRENT_TARGET} "-framework Metal -framework MetalKit")
        IF (GX_IN_IOS)
            TARGET_LINK_LIBRARIES(${GX_CURRENT_TARGET} "-framework UIKit")
        ELSE (GX_IN_IOS)
            TARGET_LINK_LIBRARIES(${GX_CURRENT_TARGET} "-framework Cocoa")
        ENDIF (GX_IN_IOS)
    ENDIF (APPLE)
    TARGET_LINK_LIBRARIES(${GX_CURRENT_TARGET} GearoenixGameEngine)
    IF (GX_IN_WINDOWS)
        SET(GX_ASSET_DIR $<TARGET_FILE_DIR:${GX_CURRENT_TARGET}>/assets)
        ADD_CUSTOM_COMMAND(
                TARGET ${GX_CURRENT_TARGET} POST_BUILD
                COMMAND ${CMAKE_COMMAND} -E copy_if_different ${GX_SDK_LIBS_PATH}/fmod.dll $<TARGET_FILE_DIR:${GX_CURRENT_TARGET}>)
    ELSEIF (GX_IN_ANDROID)
        SET(GX_ASSET_DIR ${CMAKE_SOURCE_DIR}/android/app/src/main/assets)
    ELSEIF (GX_IN_LINUX)
        SET(GX_ASSET_DIR $<TARGET_FILE_DIR:${GX_CURRENT_TARGET}>/assets)
        ADD_CUSTOM_COMMAND(
                TARGET ${GX_CURRENT_TARGET} POST_BUILD
                COMMAND ${CMAKE_COMMAND} -E copy_if_different ${GX_SDK_LIBS_PATH}/libfmod.so $<TARGET_FILE_DIR:${GX_CURRENT_TARGET}>)
        ADD_CUSTOM_COMMAND(
                TARGET ${GX_CURRENT_TARGET} POST_BUILD
                COMMAND ${CMAKE_COMMAND} -E copy_if_different ${GX_SDK_LIBS_PATH}/libfmod.so.13 $<TARGET_FILE_DIR:${GX_CURRENT_TARGET}>)
        ADD_CUSTOM_COMMAND(
                TARGET ${GX_CURRENT_TARGET} POST_BUILD
                COMMAND ${CMAKE_COMMAND} -E copy_if_different ${GX_SDK_LIBS_PATH}/libfmod.so.13.11 $<TARGET_FILE_DIR:${GX_CURRENT_TARGET}>)
    ENDIF ()
    # In WASM, for now, we have different approach for tha assets.
    IF (NOT GX_IN_WASM)
        ADD_CUSTOM_COMMAND(
                TARGET ${GX_CURRENT_TARGET} POST_BUILD
                COMMAND ${CMAKE_COMMAND} -E make_directory ${GX_ASSET_DIR})
        FILE(GLOB GX_ASSET_FILES ${GX_MAIN_ASSETS_PATH}/*)
        FOREACH (GX_ASSET_FILE ${GX_ASSET_FILES})
            ADD_CUSTOM_COMMAND(
                    TARGET ${GX_CURRENT_TARGET} POST_BUILD
                    COMMAND ${CMAKE_COMMAND} -E copy_if_different ${GX_ASSET_FILE} ${GX_ASSET_DIR})
        ENDFOREACH ()
        ADD_DEPENDENCIES(${GX_CURRENT_TARGET} GearoenixAssets)
    ENDIF ()
ENDFUNCTION(GX_ENGINE_PREPARE_TARGET)

SET(GX_ENGINE_SRC_DIR ${GX_ENGINE_SRC_DIR} PARENT_SCOPE)